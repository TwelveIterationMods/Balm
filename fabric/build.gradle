plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'idea'
    id 'net.darkhax.curseforgegradle'
    id "com.modrinth.minotaur"
}

base {
    archivesName = "${mod_id}-fabric-${minecraft_version}"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    implementation project(":shared")
    implementation project(":shared-bridge")

    compileOnly "org.jetbrains:annotations:22.0.0"
}

apply from: 'dependencies.gradle'

loom {
    if (project(":shared").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":shared").file("src/main/resources/${mod_id}.accesswidener"))
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        data {
            inherit client
            setConfigName("fabric Data")
            ideConfigGenerated(true)
            runDir("build/datagen")

            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":shared").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"
        }
    }
}

sourceSets.main.resources.srcDir('src/generated/resources')

tasks.withType(JavaCompile).configureEach {
    source(project(":shared").sourceSets.main.allSource)
    source(project(":shared-bridge").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":shared").sourceSets.main.allJava)
    source(project(":shared-bridge").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":shared").sourceSets.main.allSource)
    from(project(":shared-bridge").sourceSets.main.allSource)
}

processResources {
    from project(":shared").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = mod_id + "-fabric"
            version = project.version + (!project.version.endsWith("SNAPSHOT") ? "+" + minecraft_version : "")

            from components.java
        }
    }

    repositories {
        maven {
            var releasesRepoUrl = "https://maven.twelveiterations.com/repository/maven-releases/"
            var snapshotsRepoUrl = "https://maven.twelveiterations.com/repository/maven-snapshots/"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            name = "twelveIterationsNexus"
            credentials(PasswordCredentials)
        }
    }
}

task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    dependsOn('build')
    description = 'Publishes the Fabric build to CurseForge.'
    group = 'publishing'

    apiToken = project.findProperty("curseforge.api_key") ?: System.getenv("CURSEFORGE_TOKEN") ?: "none"

    def mainFile = upload(curseforge_fabric_project_id, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
    mainFile.changelog = rootProject.file('CHANGELOG.md').text
    mainFile.addRequirement("fabric-api")
    project.minecraft_versions.split(',').toList().each { mainFile.addGameVersion(it) }
    mainFile.releaseType = "release"
}

modrinth {
    token = project.findProperty("modrinth.token") ?: System.getenv("MODRINTH_TOKEN") ?: "none"
    projectId = project.modrinth_project_id
    versionType = project.modrinth_release_type
    versionNumber = project.version + "+fabric-" + project.minecraft_version
    uploadFile = remapJar
    changelog = rootProject.file("CHANGELOG.md").text
    gameVersions = project.minecraft_versions.split(',').toList()
    syncBodyFrom = rootProject.file("modrinth.md").text
    loaders = ['fabric']
    dependencies {
        required.project "fabric-api"
    }
}